<html>
  <head>
    <title>Dancing Nebula</title>
  </head>
  <body>
    <div class="input">Choose a song</div>
    <div class="default">Play default song</div>

    <div class="spinner">
      <svg width="64px" height="64px" xmlns="http://www.w3.org/2000/svg" viewBox="0 0 100 100" preserveAspectRatio="xMidYMid">
        <rect x="0" y="0" width="100" height="100" fill="none" class="bk"></rect>
        <circle cx="50" cy="50" r="40" stroke="none" fill="none" stroke-width="10" stroke-linecap="round"></circle>
        <circle cx="50" cy="50" r="40" stroke="#fff" fill="none" stroke-width="6" stroke-linecap="round">
          <animate attributeName="stroke-dashoffset" dur="1.5s" repeatCount="indefinite" from="0" to="502"></animate>
          <animate attributeName="stroke-dasharray" dur="1.5s" repeatCount="indefinite" values="150.6 100.4;1 250;150.6 100.4"></animate>
        </circle>
      </svg>
    </div>

    <div id="main"></div>

    <script type="x-shader/x-vertex" id="clear-vertexshader">
      void main() {
        // On cherche à appliquer un filtre noir translucide sur l'écran
        // afin d'atténuer légèrement le dessin de l'écran à chaque frame.
        // On n'applique donc aucune matrice de transformation sur notre position.
        // Ainsi on a un genre de flou cinétique pour pas cher.
        gl_Position = vec4(position, 1.0);
      }
    </script>

    <script type="x-shader/x-fragment" id="clear-fragmentshader">
      void main() {
        // On applique un noir léger à l'écran
        gl_FragColor = vec4(0.0, 0.0, 0.0, 0.06);
      }
    </script>

    <script type="x-shader/x-vertex" id="vertexshader">
      uniform float amount;
      uniform float resolution;
      uniform float radius;
      uniform float time;
      uniform float average;
      uniform float thetaFactor;

      varying vec4 color;

      float rand(vec2 co){
          return fract(sin(dot(co.xy ,vec2(12.9898,78.233))) * 43758.5453);
      }

      void main() {
        // rang de la courbe courante parmi les courbes
        float curveOffset = position[0];

        // rang du point courant parmi les points de la courbe courante
        float pointOffset = position[1];

        // Amplitude du signal pour la fréquence correspondant au point courant.
        float magnitude = position[2];

        // Rayon de la spirale pondérée avec le rang du point
        // (resolution est le nombre de points par courbe, pointOffset/resolution est donc un
        // un nombre qui varie de façon linéaure entre 0 et 1, ainsi que le veut la spirale d'archimède).
        float localRadius = pointOffset/resolution*radius;

        // L'angle du point par rapport à l'origine du cercle trigonométrique dépend du rang du point
        // Il est proportionnel à l'amplitude du signal pour la fréquence correspondant au point.
        // Il est proportionnel à un facteur arbitraire "thetaFactor".
        // Enfin il varie en fonction du temps (la constante 1.5707963268 est simplement un multiple de pi
        // permettant d'initialiser la courbure de la spirale à une certaine valeur).
        // En somme, plus thetaFactor est important, et plus le facteur temps (composé avec une sinusoïde) est important,
        // plus la courbe s'enroule sur elle même.
        float theta = (magnitude + 1.0)*(pointOffset + curveOffset)*thetaFactor*((sin(time/1000.0 - 1.5707963268) + 1.0)/2.0*2.0 + 0.8);

        // Une courbe sur deux est projetée vers le bas, l'autre vers le haut. L'amplitude de la courbe sur les ordonnées
        // dépend de l'amplitude du signal pour la fréquence correspondant au point, mais également du rang de la courbe elle même parmi
        // les autres courbes.
        float y = (mod(curveOffset, 2.0) == 0.0 ? 1.0 : -1.0)*(average*3.0 + curveOffset*4.0*(0.01 + (time <= 2000.0 ? sin(time/540.0 + 1.0)*10.0 + 10.1 : 0.1) + magnitude*(1.0 + magnitude*15.0))*(0.8 + 5.0*(magnitude + average))/amount);
        float curveFactor = curveOffset/100.0;

        // Conversion de coordonées polaires on coordonnées cartésiennes
        // Le rayon courant est proportionnel au rang du point dans sa courbe (équation de la spirale d'archimède)
        // On ajoute un terme qui varie en sinus/cosinus du temps, de l'amplitude du signal pour la fréquence
        // correspondant au point, et du rang du point dans sa courbe.
        gl_Position = projectionMatrix * modelViewMatrix * vec4(
          localRadius*sin(theta) + 0.3*sin(time/50.0*(magnitude + 1.0) + pointOffset)/(0.5/magnitude),
          y,
          localRadius*cos(theta) + 0.3*cos(time/50.0*(magnitude + 1.0) + pointOffset)/(0.5/magnitude),
          1.0
        );

        // On ajuste la taille du point on fonction de sa distance avec la caméra.
        // On fixe un passe-bas car autrement c'est laid.
        gl_PointSize = 1.0/distance(gl_Position.xyz, modelViewMatrix[3].xyz);
        if(gl_PointSize > 0.8) gl_PointSize = 0.8;

        // La couleur de la courbe varie simplement selon le temps composé avec une sinusoïde.
        // En mettant des facteurs différents pour chaque composantes, la variation semble moins homogène
        // et plus orgnanique.
        // La composantes alpha est d'autant plus faible que le point est proche de l'extrémité de la courbe,
        // ainsi les courbes ne semblent pas s'arrêter net mais s'attenuent progressivement vers le noir.
        color = vec4(
          sin(curveFactor + time/50.0)*0.15 + 0.7,
          sin(curveFactor + time/100.0)*0.15 + 0.7,
          sin(curveFactor + time/200.0)*0.15 + 0.7,
          ((1.0 - curveOffset/amount) - (1.0 - pointOffset/resolution))*0.1
        );
      }
    </script>

    <script type="x-shader/x-fragment" id="fragmentshader">
      varying vec4 color;

      void main() {
        gl_FragColor = color;
      }
    </script>
  </body>
</html>
